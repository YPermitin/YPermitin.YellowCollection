
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьПривилегированныйРежим(Истина);
	
	МассивВыбранныхПользователей = Новый Массив;
	Если Параметры.Свойство("МассивПользователей") Тогда
		МассивВыбранныхПользователей = Параметры.МассивПользователей;
	КонецЕсли;
	
	КоллекцияПользователей = ПользователиИнформационнойБазы.ПолучитьПользователей();
	Для Каждого ЭлементПользователь Из КоллекцияПользователей Цикл		
		НовыйЭлемент = СписокПользователей.Добавить(
			ЭлементПользователь.Имя,
			ЭлементПользователь.Имя + " (" + ЭлементПользователь.ПолноеИмя + ")");
		Если НЕ МассивВыбранныхПользователей.Найти(ЭлементПользователь.Имя) = Неопределено Тогда
			НовыйЭлемент.Пометка = Истина;
		КонецЕсли;
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ВыборСделан Тогда
		
		МассивВыбранныхЭлементов = Новый Массив;
		Для Каждого ЭлементВыбора Из СписокПользователей Цикл
			Если НЕ ЭлементВыбора.Пометка Тогда
				Продолжить;
			КонецЕсли;
			МассивВыбранныхЭлементов.Добавить(ЭлементВыбора.Значение);
		КонецЦикла;
			
		ОповеститьОВыборе(МассивВыбранныхЭлементов);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьВыделенные(Команда)
	
	ВыборСделан = Истина;
	Закрыть();
	
КонецПроцедуры

#КонецОбласти


