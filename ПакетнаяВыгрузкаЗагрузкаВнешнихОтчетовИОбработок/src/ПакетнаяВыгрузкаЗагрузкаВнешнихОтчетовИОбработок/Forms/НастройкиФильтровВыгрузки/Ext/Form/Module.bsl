
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	АдресСхемы = Неопределено;
	Если НЕ Параметры.Свойство("АдресСхемы", АдресСхемы) Тогда
		
		ВызватьИсключение НСтр("ru = 'Не передан адрес схемы компоновки.'");
		
	КонецЕсли;
	
	ДоступныеНастройки = Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемы);	
	КомпоновщикНастроек.Инициализировать(ДоступныеНастройки);
	
	АдресНастройкиФильтров = Неопределено;
	Если Параметры.Свойство("АдресНастройкиФильтров", АдресНастройкиФильтров) Тогда
		
		НастройкиФильтров = ПолучитьИзВременногоХранилища(АдресНастройкиФильтров);
		
		Если НЕ НастройкиФильтров = Неопределено Тогда
		
			КомпоновщикНастроек.ЗагрузитьНастройки(НастройкиФильтров);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
		
	Если ВыборСделан Тогда
		
		ОповеститьОВыборе(АдресНастроек);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	СохранитьАдресНастроек();
	ВыборСделан = Истина;
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура СохранитьАдресНастроек()
	
	НастройкиФильтров = КомпоновщикНастроек.ПолучитьНастройки();	
	
	АдресНастроек = ПоместитьВоВременноеХранилище(НастройкиФильтров, УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти